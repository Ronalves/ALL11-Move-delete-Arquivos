*----------------------------------------------------------------------*
*                          GLOBAL HITSS                                *
*----------------------------------------------------------------------*
*Cliente....: CLARO                                                    *
*Autor......: Ronaldo Aparecido Alves - Z221452                        *
*Funcional..: Márcio Roberto Lima                                      *
*Data.......: 03/06/2025                                               *
*Programa...: ZRFI984                                                  *
*Descrição..: Criação de programa para movimentação de Arquivos nas    *
*             pastas do S4/HANA                                        *
*Transação..: ZTFI859                                                  *
*Projeto....: S/4 HANA                                                 *
*Modulo.....: FI                                                       *
*----------------------------------------------------------------------*
*                    Histórico das Modificações                        *
*----------------------------------------------------------------------*
* Request    Responsável     Data       Descrição                      *
*----------------------------------------------------------------------*
REPORT zrfi984 NO STANDARD PAGE HEADING  LINE-COUNT 65
                                         LINE-SIZE 80.
*********
**Tipos**
*********
TYPES: BEGIN OF ty_arq,
           line(10000),
       END OF ty_arq.

TYPES: BEGIN OF ty_string,
           line(50) TYPE c,
       END OF ty_string.


********************
**Tabelas Internas**
********************
DATA: tg_dir_list TYPE STANDARD TABLE OF epsfili,
      tg_arq      TYPE TABLE OF ty_arq,
      tg_string   TYPE TABLE OF ty_string,
      tg_string2  TYPE TABLE OF ty_string.

*************
**Workareas**
*************
DATA: wg_dir_list TYPE epsfili,
      wg_arq      TYPE ty_arq,
      wg_string   TYPE ty_string,
      wg_string2  TYPE ty_string.

*************
**Variáveis**
*************
DATA:vg_name          TYPE epsfili-name,
     vg_contador(10)  TYPE c,
     vg_movido(3)     TYPE c,
     vg_lines(10)     TYPE c,
     vg_new_name      TYPE epsfili-name.

**************
**Constantes**
**************
CONSTANTS:c_10(02) TYPE c VALUE '10',
          c_cp(2)  TYPE c VALUE 'CP',
          c_i(1)   TYPE c VALUE 'I',
          c_sim(3) TYPE c VALUE 'SIM'.

*******************
**Tela de Seleção**
*******************
SELECTION-SCREEN BEGIN OF BLOCK bl1 WITH FRAME TITLE text-001.
PARAMETERS:p_origem  TYPE rlgrap-filename, "Diretório de Origem
           p_nomeo   TYPE rlgrap-filename, "Nome do Arquivo de Origem
           p_dest    TYPE rlgrap-filename, "Diretório de Destino
           p_rename  AS CHECKBOX USER-COMMAND a1,"Renomear Arquivo de Destino?
           p_nomed   TYPE rlgrap-filename."Nome Arquivo Destino
SELECTION-SCREEN END OF BLOCK bl1.

*******************
**Eventos de Tela**
*******************
AT SELECTION-SCREEN OUTPUT.
  LOOP AT SCREEN.
    IF screen-name = 'P_NOMED'.
      screen-input = 0.
    ENDIF.
    MODIFY SCREEN.
  ENDLOOP.

  IF p_rename IS NOT INITIAL.
    LOOP AT SCREEN.
      IF screen-name = 'P_NOMED'.
        screen-input = 1.
      ENDIF.
      MODIFY SCREEN.
    ENDLOOP.
  ELSE.
    CLEAR p_nomed.
  ENDIF.

**********
**Início**
**********
START-OF-SELECTION.

  PERFORM valida_tela.
  PERFORM le_diretorio_origem.
  PERFORM filtra_arquivos.
  PERFORM move_arquivos.


*---------------------------------------------------------------------*
*       END-OF-SELECTION.                                             *
*---------------------------------------------------------------------*
END-OF-SELECTION.

*&---------------------------------------------------------------------*
*&      Form  LE_DIRETORIO_ORIGEM
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM le_diretorio_origem.

  DATA: lv_dir_name  TYPE epsf-epsdirnam.
  DATA: lv_file_mask TYPE epsf-epsfilnam.

  lv_dir_name = p_origem.

  TRANSLATE lv_dir_name TO LOWER CASE.

  CALL FUNCTION 'EPS_GET_DIRECTORY_LISTING'
    EXPORTING
      dir_name               = lv_dir_name
      file_mask              = lv_file_mask
    TABLES
      dir_list               = tg_dir_list
    EXCEPTIONS
      invalid_eps_subdir     = 1
      sapgparam_failed       = 2
      build_directory_failed = 3
      no_authorization       = 4
      read_directory_failed  = 5
      too_many_read_errors   = 6
      empty_directory_list   = 7
      OTHERS                 = 8.
  IF sy-subrc NE 0.
*    Erro ao Ler Diretório de Origem
    MESSAGE e000(zmm) WITH text-008 lv_dir_name.
  ENDIF.

ENDFORM.                    " ZF_PROCESS
*&---------------------------------------------------------------------*
*&      Form  ZF_OPEN_ARQ_SERV
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM zf_open_arq_serv USING p_name_arq  TYPE epsfilnam
                            p_diretorio TYPE epsf-epsdirnam.

  DATA: lv_filename  TYPE string.

  REFRESH: tg_arq.
  CLEAR: wg_arq.

  CONCATENATE p_diretorio p_name_arq
         INTO lv_filename
         SEPARATED BY '/'.

  OPEN DATASET lv_filename FOR INPUT IN TEXT MODE ENCODING NON-UNICODE.

  IF sy-subrc IS INITIAL.

    DO.

      READ DATASET lv_filename INTO wg_arq.

      IF sy-subrc IS NOT INITIAL.
        EXIT.
      ENDIF.

      APPEND wg_arq TO tg_arq.
      CLEAR: wg_arq.

    ENDDO.

  ENDIF.

  CLOSE DATASET lv_filename.

ENDFORM.                    " ZF_OPEN_ARQ_SERV
*&---------------------------------------------------------------------*
*&      Form  ZF_INPUT_DIR_FILE
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM zf_input_dir_file USING p_path     TYPE rlgrap-filename
                             p_name_arq TYPE epsfilnam.

  DATA: lv_dir_name TYPE epsf-epsdirnam.
  DATA: lv_filename  TYPE string.

  FIELD-SYMBOLS: <fs_data> LIKE LINE OF tg_arq.

  CLEAR:vg_movido.

  lv_dir_name = p_path.

  TRANSLATE lv_dir_name TO LOWER CASE.

  CONCATENATE lv_dir_name p_name_arq
         INTO lv_filename
         SEPARATED BY '/'.

  CHECK lv_filename IS NOT INITIAL.

  OPEN DATASET lv_filename FOR OUTPUT IN TEXT MODE ENCODING NON-UNICODE.
  IF sy-subrc = 0.

    LOOP AT tg_arq ASSIGNING <fs_data>.
      TRANSFER <fs_data> TO lv_filename.
    ENDLOOP.

    UNASSIGN: <fs_data>.
    CLOSE DATASET lv_filename.

    ADD 1 TO vg_contador.
    vg_movido = c_sim.

  ELSE.

    MESSAGE e000(zmm) WITH text-010 lv_filename.

  ENDIF.

ENDFORM.                    " ZF_INPUT_DIR_FILE
*&---------------------------------------------------------------------*
*&      Form  VALIDA_TELA
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM valida_tela .
  IF p_origem IS INITIAL.
*   Preencher Diretório de Origem
    MESSAGE e000(zmm) WITH text-003.
  ENDIF.

  IF p_nomeo IS INITIAL.
*   Preencher Nome do Arquivo de Origem
    MESSAGE e000(zmm) WITH text-005.
  ENDIF.

  IF p_dest IS INITIAL.
*   Preencher Diretório de Destino
    MESSAGE e000(zmm) WITH text-006.
  ENDIF.

  IF p_rename IS NOT INITIAL AND p_nomed IS INITIAL.
*   Preencher Nome Arquivo Destino
    MESSAGE e000(zmm) WITH text-007.
  ENDIF.

ENDFORM.                    " VALIDA_TELA
*&---------------------------------------------------------------------*
*&      Form  FILTRA_ARQUIVOS
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM filtra_arquivos .

  DATA:rl_files TYPE RANGE OF epsfili-name,
       wl_files LIKE LINE OF rl_files,
       vl_tabix TYPE sy-tabix.

  CLEAR:rl_files[],wl_files,vl_tabix.
  IF tg_dir_list[] IS NOT INITIAL.

    wl_files-option = c_cp.
    wl_files-sign = c_i.
    wl_files-low = p_nomeo.
    APPEND wl_files TO rl_files.

    LOOP AT tg_dir_list INTO wg_dir_list.
      vl_tabix = sy-tabix.
      IF NOT wg_dir_list-name IN rl_files[].
        DELETE tg_dir_list INDEX vl_tabix.
      ENDIF.
    ENDLOOP.

    IF tg_dir_list[] IS INITIAL.
*   Arquivo(s) não Encontrado(s)
      MESSAGE e000(zmm) WITH text-009.
    ENDIF.

  ELSE.
*   Arquivo(s) não Encontrado(s)
    MESSAGE e000(zmm) WITH text-009.
  ENDIF.

ENDFORM.                    " FILTRA_ARQUIVOS
*&---------------------------------------------------------------------*
*&      Form  MOVE_ARQUIVOS
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM move_arquivos .

  DATA: lv_dir_name TYPE epsf-epsdirnam.

  lv_dir_name = p_origem.

  CLEAR: vg_contador,vg_lines.

  DESCRIBE TABLE tg_dir_list LINES vg_lines.

  IF vg_lines > 1 AND p_nomed IS NOT INITIAL.
    MESSAGE e000(zmm) WITH text-011 text-012.
  ELSEIF vg_lines = 1 AND p_nomed IS NOT INITIAL.
    READ TABLE tg_dir_list INTO wg_dir_list INDEX 1.
    IF sy-subrc = 0.
      IF wg_dir_list-name CS '.' AND p_nomed CS '*.'.
        CLEAR:tg_string,wg_string.
        SPLIT wg_dir_list-name AT '.' INTO TABLE tg_string.
        READ TABLE tg_string INTO wg_string INDEX 1.
        IF sy-subrc = 0.

          CLEAR:tg_string2,wg_string2.
          SPLIT p_nomed AT '.' INTO TABLE tg_string2.
          READ TABLE tg_string2 INTO wg_string2 INDEX 2.
          IF sy-subrc = 0.
            CONCATENATE wg_string-line wg_string2-line INTO vg_new_name SEPARATED BY '.'.
          ENDIF.
        ENDIF.
      ENDIF.
    ENDIF.

  ENDIF.

  LOOP AT tg_dir_list INTO wg_dir_list.

    CLEAR:vg_name.

    vg_name = p_nomed.

    IF vg_new_name IS NOT INITIAL.
      vg_name = vg_new_name.
    ENDIF.

    PERFORM zf_open_arq_serv USING wg_dir_list-name
                                   lv_dir_name.

    IF vg_name IS INITIAL.
      PERFORM zf_input_dir_file USING p_dest
                                      wg_dir_list-name.
    ELSE.
      PERFORM zf_input_dir_file USING p_dest
                                      vg_name.
    ENDIF.

    IF vg_movido = c_sim.
      PERFORM deleta_arqu_origem USING wg_dir_list-name
                                   lv_dir_name.
    ENDIF.

  ENDLOOP.

  MESSAGE s000(zmm) WITH text-002 vg_contador text-004.


ENDFORM.                    " MOVE_ARQUIVOS
*&---------------------------------------------------------------------*
*&      Form  DELETA_ARQU_ORIGEM
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM deleta_arqu_origem USING p_name_arq  TYPE epsfilnam
                              p_diretorio TYPE epsf-epsdirnam.

  IF p_name_arq IS NOT INITIAL AND p_diretorio IS NOT INITIAL.

    CALL FUNCTION 'EPS_DELETE_FILE'
      EXPORTING
        file_name              = p_name_arq
        dir_name               = p_diretorio
      EXCEPTIONS
        invalid_eps_subdir     = 1
        sapgparam_failed       = 2
        build_directory_failed = 3
        no_authorization       = 4
        build_path_failed      = 5
        delete_failed          = 6
        OTHERS                 = 7.
    IF sy-subrc <> 0.
      MESSAGE e000(zmm) WITH text-013.
    ENDIF.

  ENDIF.

ENDFORM.                    " DELETA_ARQU_ORIGEM
